;===============================================================================
; INTELIGENCIA ARTIFICIAL:
;===============================================================================
[Statedef -2]

[State 0, Run]
type = ChangeState
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/2
value = 60

[State 0, Run Atras]
type = ChangeState
triggerall = backedgebodydist > 50
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 100 && p2movetype = A
trigger1 = random < (ailevel * 100)/4
value = 70

[State 0, Guard]
type = ChangeState
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = (p2bodydist x < 50 && p2movetype = A) || (enemy,numproj > 0)
trigger1 = random < (ailevel * 100)/2
value = ifelse(statetype = A,132,130)

[State 0, Power Charge]
type = ChangeState
triggerall = statetype != A
triggerall = power < 3000
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/4
value = 500

[State 0, Power Charge End]
type = ChangeState
triggerall = time >= 12
triggerall = stateno = 500
triggerall = ailevel > 0
trigger1 = (p2bodydist x < 50) || (enemy,numproj > 0)
trigger2 = power = 3000
value = 501

[State 0, A]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 200

[State 0, B]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 300

[State 0, C]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [50,200]
trigger1 = random < (ailevel * 100)/8
value = 400

[State 0, A/B/C Aire]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 610

[State 0, Push!]
type = ChangeState
triggerall = power >= 500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)/2
value = 1000

[State 0, Control]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)/2
value = 1100

[State 0, Big Wind]
type = ChangeState
triggerall = numhelper(1250) = 0
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x < 50
trigger1 = random < (ailevel * 100)/2
value = 21200

[State 0, Bind Skill]
type = ChangeState
triggerall = numhelper(21350) = 0
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 50
trigger1 = p2movetype != A && random < (ailevel * 100)/8
trigger2 = p2movetype = A && random < (ailevel * 100)/2
value = 21300

[State 0, Girder Throw]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)
value = 21100

[State 0, Weight Rocks]
type = ChangeState
triggerall = numhelper(1550) = 0
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [75,150]
trigger1 = random < (ailevel * 100)
value = 1500

[State 0, Hell Storm]
type = ChangeState
triggerall = power >= 3000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 50
trigger1 = random < (ailevel * 100)*2
value = 3000
;===============================================================================
;---------------------------Estados usados Siempre------------------------------
;===============================================================================
[Statedef -3]

[State 0, HELPER ESCALA]
type = Helper
trigger1 = NUMHELPER(999999) = 0
helpertype = normal
name = "HELPER ESCALA"
ID = 999999
stateno = 999999
pos = 9999,9999
postype = P1
facing = 1
keyctrl = 0
ownpal = 1
supermove = 999999
pausemove = 999999
supermovetime = 999999
pausemovetime = 999999
;===============================================================================
; Pared Rota:
;-------------------------------------------------------------------------------
[State 0, Helper]
type = Helper
triggerall = p2movetype = H
triggerall = numhelper(1061) = 0
trigger1 = p2stateno = 1056
helpertype = normal
name = "Pared Rota"
ID = 1061
stateno = 1061
pos = 0,-30
postype = p2
facing = 1
keyctrl = 0
ownpal = 1
;===============================================================================
; Polvo en P2:
;-------------------------------------------------------------------------------
[State 0, Helper]
type = Helper
triggerall = p2movetype = H
triggerall = enemy,pos y >= 0
triggerall = numhelper(1065) = 0
trigger1 = p2stateno = 1054
helpertype = normal
name = "Polvo en P2"
ID = 1065
stateno = 1065
pos = 0,0
postype = p1
facing = 1
keyctrl = 0
ownpal = 1
;===============================================================================
; Limitador Air Jump:
;-------------------------------------------------------------------------------
[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 0 && (stateno = 65||stateno = 70)
v = 5
value = 1

[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 1 && pos y >= 0
v = 5
value = 0
;-------------------------------------------------------------------------------
; Sonidos Comunes:
;-------------------------------------------------------------------------------
[State 0, Saltando]
type = PlaySnd
triggerall = stateno = 40
trigger1 = Time = 1
value = S40,1
volume = 55
channel = -1

[State 0, Super Jump]
type = PlaySnd
triggerall = stateno = 80
trigger1 = Time = 1
value = S60,1
volume = 55
channel = -1

[State 0, Land]
type = PlaySnd
triggerall = stateno = 52
trigger1 = time = 1
value = S52,1
volume = 50

[State 0, Run FWD]
type = PlaySnd
triggerall = stateno = 60 || stateno = 70
trigger1 = time = 1
value = S5,45
volume = 50

[State 0, RemoveExplod]
type = RemoveExplod
TRIGGERALL = stateno != 500
trigger1 = time = 1
id = 550

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 1

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 2

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 3

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
triggerall = stateno != 501
trigger1 = time = 1
channel = 15
[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
triggerall = stateno != 501
trigger1 = time = 1
channel = 16

[State 0, Golpeado]
type = PlaySnd
trigger1 = movetype=H && (stateno!=[0,155]) && !time && HitOver=0 && random%3=0
value = 5000,ifelse(random < 500,1,ifelse(random < 500,2,1))

[State 0, Solucion Stand Aire]
type = ChangeState
triggerall = stateno = 0||stateno = 20
trigger1 = pos y < 0
value = 50
ctrl = 1